### 15.2

A “palindrome” is a sentence that reads the same backward as forward. Write a predicate `palindrome?` that takes a sentence as argument and decides whether it is a palindrome. For example:

~~~ scheme
> (palindrome? '(flee to me remote elf))
#t

> (palindrome? '(flee to me remote control))
#f
~~~

Do not reverse any words or sentences in your solution.

***

The procedure is going to compare the first and last letters of the argument, ignoring spaces. This suggests that the argument sentence should first be collapsed into a single word, either by using a recursive procedure …

~~~ scheme
(define (sentence-to-word sent)
  (if (empty? sent)
      ""
      (word (first sent) (sentence-to-word (bf sent)))))
~~~

… or by using `accumulate`, i.e. `(accumulate word sent)`.

The `palindrome?` procedure needs to check that the first and last letters of the word are the same, returning `#f` if they aren’t, and otherwise recursively checking the remainder of the word less its first and last letters.

The base case is a word of length `0` or `1`, which returns `#t`.

~~~ scheme
(define (palindrome? sent)
  (palindrome-helper (accumulate word sent)))

(define (palindrome-helper wd)
  (if (<= (count wd) 1)
      #t
      (if (equal? (first wd) (last wd))
          (palindrome-helper (word (bf (bl wd))))
          #f)))
~~~

Recalling the property of `and` that it returns either `#f` or the value of the final argument supplied, `palindrome-helper` can be written more elegantly:

~~~ scheme
(define (palindrome-helper wd)
  (if (<= (count wd) 1)
      #t
      (and (equal? (first wd) (last wd))
           (palindrome-helper (word (bf (bl wd)))))))
~~~