### 15.5

Suppose you have a phone number, such as 223-5766, and you'd like to figure out a clever way to spell it in letters for your friends to remember. Each digit corresponds to three possible letters. For example, the digit 2 corresponds to the letters A, B, and C. Write a procedure that takes a number as argument and returns a sentence of all the possible spellings:

~~~ scheme
> (phone-spell 2235766)
(aadjpmm aadjpmn … ccflsoo)
~~~

(We’re not showing you all 2187 words in this sentence.) You may assume there are no zeros or ones in the number, since those don't have letters.

Hint: This problem has a lot in common with the subsets example.

***

First, `number-to-letters` takes a number as argument and returns the 3- or 4-letter word of the letters corresponding to that number on the phone keypad.

~~~ scheme
(define (number-to-letters n)
  (if (< n 2)
      n
      (item n '(- abc def ghi jkl mno pqrs tuv wxyz))))
~~~

(Despite what it says in the preamble, it’s simple enough to deal with `1` or `0` – given these numbers, the procedure returns them verbatim.)

Further progress was beyond me, until I referred to [buntine’s solution](https://github.com/buntine/Simply-Scheme-Exercises/blob/master/15-advanced-recursion/15-5.scm). 

The way to think about this is to take the last digit and find the sentence of all possible words it can spell.

Take for example `23`. The digit `3` corresponds to the word `'def`. The procedure `prepend-each` uses the `prepend-every` procedure (see Exercise 9.5) to make a sentence from the word:

~~~ scheme
> (prepend-each 'def '(""))    ;; i.e. “3”
(d e f)
~~~

The second-to-last digit, `2`, corresponds to `'abc`. The procedure `prepend-each` returns a sentence of the words made up of each letter of `'abc` prepended to each word in the sentence `'(d e f)`.

~~~ scheme
(define (prepend-each wd sent)
  (if (empty? wd)
      '()
      (se (prepend-every (first wd) sent)
          (prepend-each (bf wd) sent))))
          
(define (prepend-every letter sent)
  (if (empty? sent)
      '()
      (se (word letter (first sent)) (prepend-every letter (bf sent)))))

> (prepend-each 'abc '(d e f))   ;; i.e. “2” and “3”
(ad ae af 
 bd be bf
 cd ce cf)
~~~

Then `phone-spell` uses `prepend-each` to build up the sentence of combinations. 

The base case is a sentence of an empty word (rather than an empty sentence) because this will be supplied as the second argument to `prepend-every` – compare the result of `(prepend-every 'a '(""))` with `(prepend-every 'a '())`.

~~~ scheme
(define (phone-spell n)
  (se (if (empty? n)
          ""
          (prepend-each (number-to-letters (first n))
                        (phone-spell (bf n))))))
~~~

(Final note: the phone number given as an example in the preamble, `2235766`, spells out `abelson`!)