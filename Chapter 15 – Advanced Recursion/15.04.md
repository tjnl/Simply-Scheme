### 15.4

Write a predicate procedure `substring?` that takes two words as arguments and returns `#t` if and only if the first word is a substring of the second. (See Exercise 15.3 for the definition of a substring.)

Be careful about cases in which you encounter a “false start,” like this:

~~~ scheme
> (substring? 'ssip 'mississippi)
#t
~~~

and also about subsets that don't appear as consecutive letters in the second word:

~~~ scheme
> (substring? 'misip 'mississippi)
#f
~~~

***

It would be possible to define the procedure in terms of the `substrings` procedure in Exercise 15.3:

~~~ scheme
(define (substring? wd string)
  (member? string (substrings wd)))
~~~

… but that doesn’t seem very efficient.

The recursive predicate procedure `match-from-start?` compares each letter of `string` with each letter of `wd`:

* If `string` is empty, or longer than `wd`, or its first letter doesn’t match the first letter of `wd`, this is the base case that returns `#f`.
* If `string` is only one letter long, and is a match for the first letter of `wd`, this is the base case that returns `#t`.
* Otherwise, `match-from-start?` is called recursively with the `butfirst` of both `string` and `wd`.

Using this helper procedure, `substring?` can now check whether a desired `string` is found within the target `wd`:

* If `wd` is empty, there can be no match, so this is the base case that returns `#f`
* If `match-from-start?` returns `#t`, this is the base case that returns `#t`
* Otherwise, `substring?` is called recursively with the `butfirst` of `wd`.

~~~ scheme
(define (substring? string wd)
  (cond ((empty? wd) #f)
        ((match-from-start? string wd) #t)
        (else (substring? string (bf wd)))))
          
(define (match-from-start? string wd)
  (let ((string-length (count string)))
    (cond ((or (= string-length 0)
               (> string-length (count wd))
               (not (equal? (first string) (first wd))))
           #f)
          ((and (= string-length 1)
                (equal? (first string) (first wd)))
           #t)
          (else (match-from-start? (bf string) (bf wd))))))
~~~

The recursive calls are in tail-position. Recalling the properties of `and` and `or` that they return either a boolean or the value of the final argument, this means the two procedures can be rewritten without using conditionals:

~~~ scheme
(define (substring? string wd)
  (and (not (empty? wd))
       (or (match-from-start? string wd)
           (substring? string (bf wd)))))

(define (match-from-start? string wd)
  (let ((string-length (count string)))
    (and (not (or (= string-length 0)
                  (> string-length (count wd))
                  (not (equal? (first string) (first wd)))))
         (or (and (= string-length 1)
                  (equal? (first string) (first wd)))
             (match-from-start? (bf string) (bf wd))))))
~~~

Although, after further reading, it seems that `and`, `or` and `cond` are all defined using `if` anyway, so this doesn’t seem likely to be any more efficient – and it’s definitely a bit harder to read.