### 12.6 [8.11]

Write a GPA procedure. It should take a sentence of grades as its argument and return the corresponding grade point average.

~~~ scheme
> (gpa '(A A+ B+ B))
3.67
~~~

Hint: write a helper procedure `base-grade` that takes a grade as argument and returns 0, 1, 2, 3, or 4, and another helper procedure `grade-modifier` that returns –.33, 0, or .33, depending on whether the grade has a minus, a plus, or neither.

***

We recycle the two helper procedures from 8.11:

~~~ scheme
(define (base-grade grade)
  (let ((letter (first grade)))
    (cond ((equal? letter 'a) 4)
          ((equal? letter 'b) 3)
          ((equal? letter 'c) 2)
          ((equal? letter 'd) 1)
          ((equal? letter 'f) 0)
          (else 'undefined-grade))))

(define (grade-modifier grade)
  (let ((suffix (bf grade)))
    (cond ((equal? suffix '+) 0.33)
          ((equal? suffix '-) -0.33)
          ((equal? suffix "") 0)
          (else 'undefined-grade-modifier))))  
~~~

We revisit the `addup` procedure from 12.1 to create a recursive helper procedure, `add-grade-values`, which converts a sentence of grades to a number that is the total of their numeric values.

~~~ scheme
(define (add-grade-values sent)
  (if (empty? sent)
      0
      (let ((grade (first sent)))
        (+ (+ (base-grade grade) (grade-modifier grade)) (add-grade-values (bf sent))))))
~~~

Then we write the procedure `gpa` to calculate the average of the values returned by `add-grade-values`:

~~~ scheme
(define (gpa sent)
  (/ (add-grade-values sent) (count sent)))
~~~

***

NB Compared to the version of `gpa` in 8.11 which used `accumulate` …

~~~ scheme
(define (gpa grades)
  (/ (accumulate + (se (every base-grade grades) (every grade-modifier grades))) 
     (count grades)))
~~~

… we can begin to suspect that `accumulate` must be implemented along the same lines as our new recursive procedure.