### 12.12

Write a procedure `arabic` which converts Roman numerals into Arabic numerals:

~~~ scheme
> (arabic 'mcmlxxi)
1971

> (arabic 'mlxvi)
1066
~~~

You will probably find the `roman-value` procedure from Chapter 6 helpful. Don't forget that a letter can reduce the overall value if the letter that comes after it has a larger value, such as the `C` in `MCM`.

***

The `roman-value` procedure is:

~~~ scheme
(define (roman-value letter)
  (cond ((equal? letter 'i) 1)
        ((equal? letter 'v) 5)
        ((equal? letter 'x) 10)
        ((equal? letter 'l) 50)
        ((equal? letter 'c) 100)
        ((equal? letter 'd) 500)
        ((equal? letter 'm) 1000)
        (else 'huh?)))
~~~

The domain of `arabic` is words. The range is (positive) non-zero integers.

There are two base cases. If the argument to `arabic` is:

* the empty word, the return value is `0`
* a one-letter word, the return value is the `roman-value` of that letter.

(The argument may be an empty word if the recursive procedure deals with a `roman-number` such as `'iv`.)

Otherwise, we `let` the `roman-value`s of the first letter and the next letter of the argument be `this-num` and `next-num` respectively.

If `next-num` is larger than `this-num`, we subtract the latter from the former, and recursively add the result to the `arabic` of the remainder of the argument (i.e. less these first two letters, meaning in a case like `'iv`, the remainder is `""`).

Otherwise, we add `this-num` recursively to the `arabic` of the remainder of the argument (i.e. less this first letter).

~~~ scheme
(define (arabic roman-number)
  (cond ((empty? roman-number) 0)
        ((empty? (bf roman-number)) (roman-value roman-number))
        (else (let ((this-num (roman-value (first roman-number)))
                    (next-num (roman-value (first (bf roman-number)))))
                (if (> next-num this-num)
                    (+ (- next-num this-num) (arabic (bf (bf roman-number))))
                    (+ this-num (arabic (bf roman-number))))))))
~~~

***

The method above deals with the Roman numeral in one- or two-letter units (hence the two base cases). A (better!) way to manage the problem one letter at a time is to make use of the observation in the task that “a letter can reduce the overall value if the letter that comes after it has a larger value”.

Working one letter at a time, the base case is a one-letter word.

We can decide whether the value of the current letter is added or subtracted to the overall value as follows:

~~~ scheme
((if (> next-num this-num)
     -
     +)
 this-num)
~~~

If the statement is true, the result is `(- this-num)`, otherwise it is `(+ this-num)`.

~~~ scheme
(define (arabic roman-number)
  (if (= 1 (count roman-number))
      (roman-value roman-number)
      (+ (let ((this-num (roman-value (first roman-number)))
               (next-num (roman-value (first (bf roman-number)))))
           ((if (> next-num this-num)
               -
               +)
           this-num))
         (arabic (bf roman-number)))))
~~~

