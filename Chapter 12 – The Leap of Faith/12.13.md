#### 12.13

Write a new version of the `describe-time` procedure from Exercise 6.14. Instead of returning a decimal number, it should behave like this:

~~~ scheme
> (describe-time 22222)
(6 hours 10 minutes 22 seconds)

> (describe-time 4967189641)
(1 centuries 57 years 20 weeks 6 days 8 hours 54 minutes 1 seconds)
~~~

Can you make the program smart about saying `1 century` instead of `1 centuries`?

***

#### First attempt

The domain of the function is (positive) integers. The range is sentences.

The base case is a number that can be described in seconds (i.e. a number less than 60).

As in 6.14, we create a set of definitions to make things more readable (note that `year` is defined as an exact number so that our calculations don’t end up with an unwanted decimal place):

~~~ scheme
(define minute 60)                ;;             60
(define hour (* minute 60))       ;;          3,600
(define day (* hour 24))          ;;         86,400
(define week (* day 7))           ;;        604,800
(define year (* day (+ 365 1/4))) ;;    220,903,200
(define century (* year 100))     ;; 22,090,320,000
~~~

Looking at 6.14, we define a helper procedure `nearest-unit` that returns the value of the largest unit the time should be expressed in.

Then the procedure is as follows:

~~~ scheme
(define (nearest-unit n divisor)
  (floor (/ n divisor)))
  
(define (describe-time n)
  (if (< n minute)
      (se n 'seconds)
      (cond ((< n hour) (se (nearest-unit n minute) 'minutes (describe-time (remainder n minute))))
            ((< n day) (se (nearest-unit n hour) 'hours (describe-time (remainder n hour))))
            ((< n week) (se (nearest-unit n day) 'days (describe-time (remainder n day))))
            ((< n year) (se (nearest-unit n week) 'weeks (describe-time (remainder n week))))
            ((< n century) (se (nearest-unit n year) 'years (describe-time (remainder n year))))
            (else (se (nearest-unit n century) 'centuries (describe-time (remainder n century)))))))
~~~

Looking at this, we could simplify the procedure further by getting the helper procedure to perform the recursion as well:

~~~ scheme
(define (describe-time n)
  (if (<= n minute)
      (se n 'seconds)
      (cond ((<= n hour) (nearest-unit n minute 'minutes))
            ((<= n day) (nearest-unit n hour 'hours))
            ((<= n week) (nearest-unit n day 'days))
            ((<= n year) (nearest-unit n week 'weeks))
            ((<= n century) (nearest-unit n year 'years))
            (else (nearest-unit n century 'centuries)))))

(define (nearest-unit n unit-value unit-name)
  (let ((return-value (floor (/ n unit-value)))
        (remainder-value (remainder n unit-value)))
    (se return-value unit-name (describe-time remainder-value))))
~~~

***

#### Second attempt

If we would like the program to be “smart” about singular or plural units, we can use the `plural` procedure (and friends) from 6.12, simplifiying the former as appropriate so the only special case it has to manage is `century` / `centuries`…

~~~ scheme
(define (vowel? letter)
  (member? letter 'aeiou))

(define (plural noun)
  (if (and (equal? 'y (last noun)) (not (vowel? (last (bl noun)))))
      (word (bl noun) 'ies)
      (word noun 's)))
~~~

… and modifying the `nearest-unit` helper procedure to use `plural`:

~~~ scheme
(define (nearest-unit n unit-value unit-name)
  (let ((return-value (floor (/ n unit-value)))
        (remainder-value (remainder n unit-value)))
    (if (= 1 return-value)
        (se 1 unit-name (describe-time remainder-value))
        (se return-value (plural unit-name) (describe-time remainder-value)))))
~~~

The base case should also be modified to be similarly “smart”, by changing …

~~~ scheme
(se n 'seconds)
~~~

… to:

~~~ scheme
(if (= 1 n) '(1 second) (se n 'seconds))
~~~


The procedure is now:

~~~ scheme
(define (describe-time n)
  (if (<= n minute)
      (if (= 1 n) '(1 second) (se n 'seconds))
      (cond ((<= n hour) (nearest-unit n minute 'minute))
            ((<= n day) (nearest-unit n hour 'hour))
            ((<= n week) (nearest-unit n day 'day))
            ((<= n year) (nearest-unit n week 'week))
            ((<= n century) (nearest-unit n year 'year))
            (else (nearest-unit n century 'century)))))
~~~

However there ought to be a more elegant way of handling an *exact* duration, so that `(describe-time 86400)` returns `(24 hours)` rather than `(24 hours 0 seconds)`.

The base case we are looking for is “a round number (with no remainder) in the most appropriate units”. We can add this base-case to `nearest-unit` – if the `remainder-value` is `0`, then the helper procedure returns the `return-value` without further recursion. We still need to handle the “pluralisation”, but this is trivially achieved using a conditional statement.

~~~ scheme
(cond ((and (= 0 remainder-value) (= 1 return-value)) (se 1 unit-name))
      ((and (= 0 remainder-value) (< 1 return-value)) (se return-value (plural unit-name)))
      ((= 1 return-value) (se 1 unit-name (describe-time remainder-value)))
      (else (se return-value (plural unit-name) (describe-time remainder-value))))
~~~

The entire program is now:

~~~ scheme
(define minute 60)
(define hour (* minute 60))
(define day (* hour 24))
(define week (* day 7))
(define year (* day (+ 365 1/4)))
(define century (* year 100))

(define (describe-time n)
  (if (< n minute)
      (if (= 1 n) '(1 second) (se n 'seconds))
      (cond ((< n hour) (nearest-unit n minute 'minute))
            ((< n day) (nearest-unit n hour 'hour))
            ((< n week) (nearest-unit n day 'day))
            ((< n year) (nearest-unit n week 'week))
            ((< n century) (nearest-unit n year 'year))
            (else (nearest-unit n century 'century)))))

(define (nearest-unit n unit-value unit-name)
  (let ((return-value (floor (/ n unit-value)))
        (remainder-value (remainder n unit-value)))
    (cond ((and (= 0 remainder-value) (= 1 return-value)) (se 1 unit-name))
          ((and (= 0 remainder-value) (< 1 return-value)) (se return-value (plural unit-name)))
          ((= 1 return-value) (se 1 unit-name (describe-time remainder-value)))
          (else (se return-value (plural unit-name) (describe-time remainder-value))))))

(define (vowel? letter)
  (member? letter 'aeiou))

(define (plural noun)
  (if (and (equal? 'y (last noun)) (not (vowel? (last (bl noun)))))
      (word (bl noun) 'ies)
      (word noun 's)))
~~~

***

#### Third and final attempt

The way that the program deals with plurals could be refined further, so that less work is involved.

Of the list of words that require plurals, all take a final `-s` apart from `century`, so although the `plural` mechanism has been simplified it still seems like overkill.

And, in more cases than not, the output of the procedure is going to include plural rather than singular units.

So it would make sense, rather than using the `plural` procedure to make a plural from a singular (e.g. adding an `s` to `week` to make `weeks` in 51 out of 52 possible cases), instead to only “subtract” the plural ending when a singular unit is required.

We define another helper procedure `handle-plural`, which takes a number and a word as arguments, and returns a sentence:

~~~ scheme
(define (handle-plural n units)
  (if (not (= n 1))                        ;; if the number is plural (or zero)
      (se n units)                         ;; there's nothing to do!
      (se 1 (if (equal? 'c (first units))  ;; otherwise if the units are centuries
                'century                   ;; the singular is "century"
                (bl units)))))             ;; otherwise lose the last letter ("s") of units

> (handle-plural 0 'seconds)
(0 seconds)

> (handle-plural 1 'seconds)
(1 second)

> (handle-plural 2 'seconds)
(2 seconds)

> (handle-plural 1 'centuries)
(1 century)
~~~

Then we modify:

* the base case of `describe-time` from `(if (= 1 n) … )` to `(handle-plural n 'seconds)`
* the units in `describe-time` from singular to plural (e.g. `'minute` to `'minutes`)
* the conditional statement in `nearest-unit` to an `if` statement.

The final form of the program is:

~~~ scheme
(define minute 60)
(define hour (* minute 60))
(define day (* hour 24))
(define week (* day 7))
(define year (* day (+ 365 1/4)))
(define century (* year 100))

(define (describe-time n)
  (if (< n minute)
      (handle-plural n 'seconds)
      (cond ((< n hour) (nearest-unit n minute 'minutes))
            ((< n day) (nearest-unit n hour 'hours))
            ((< n week) (nearest-unit n day 'days))
            ((< n year) (nearest-unit n week 'weeks))
            ((< n century) (nearest-unit n year 'years))
            (else (nearest-unit n century 'centuries)))))

(define (nearest-unit n unit-value unit-name)
  (let ((return-value (floor (/ n unit-value)))
        (remainder-value (remainder n unit-value)))
    (if (= 0 remainder-value)
        (handle-plural return-value unit-name)
        (se (handle-plural return-value unit-name) (describe-time remainder-value)))))

(define (handle-plural n units)
  (if (not (= n 1))
      (se n units)
      (se 1 (if (equal? 'c (first units))
                'century
                (bl units)))))
~~~