### 14.16

Write a procedure `syllables` that takes a word as its argument and returns the number of syllables in the word, counted according to the following rule: the number of syllables is the number of vowels, except that a group of consecutive vowels counts as one. For example, in the word “soaring,” the group “oa” represents one syllable and the vowel “i” represents a second one.

Be sure to choose test cases that expose likely failures of your procedure. For example, what if the word ends with a vowel? What if it ends with two vowels in a row? What if it has more than two consecutive vowels?

(Of course this rule isn’t good enough. It doesn’t deal with things like silent “e”s that don’t create a syllable (“like”), consecutive vowels that don’t form a diphthong (“cooperate”), letters like “y” that are vowels only sometimes, etc. If you get bored, see whether you can teach the program to recognize some of these special cases.)

***

This is going to be an `accumulate`-type problem, with aspects of `keep`.

The initial test cases are:

* `'solo` (ends in a vowel)
* `'sloe` (ends in two vowels)
* `'quaint` (more than two consecutive vowels)


#### First attempt

The main procedure tests for the base-case of an empty word, returning `0`.

There are two recursive cases:

* If the first letter of the word is a vowel, it returns `1`, added to the result of the procedure being called recursively with whatever is left over after a helper procedure (followng the `every` pattern) is used to remove any subsquent vowels.
* Otherwise, the procedure is called with the `butfirst` of the argument.

~~~ scheme
(define (syllables wd)
  (cond ((empty? wd) 0)
        ((vowel? (first wd)) (+ 1 (syllables (strip-initial-vowels (bf wd)))))
        (else (syllables (bf wd)))))

(define (strip-initial-vowels wd)
  (if (or (empty? wd)
          (not (vowel? (first wd))))
      wd
      (strip-initial-vowels (bf wd))))

(define (vowel? letter)
  (member? letter 'aeiou))
~~~

### Second attempt

A more elegant method that avoids the use of the helper procedure is to have the first recursive case test whether an initial vowel is followed by another vowel, and only return `1` if it isn’t.

~~~ scheme
(define (syllables wd)
  (cond ((empty? wd) 0)
        ((and (vowel? (first wd))
              (or (empty? (bf wd))
                  (not (vowel? (first (bf wd))))))
         (+ 1 (syllables (bf wd))))
        (else (syllables (bf wd)))))
~~~
  