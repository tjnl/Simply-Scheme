### 14.0

A serious pitfall is failing to recognize a situation in which you need an extra variable and therefore need a helper procedure. If at each step you need the entire original argument as well as the argument thatâ€™s getting closer to the base case, you probably need a helper procedure. For example, write a procedure `pairs` that takes a word as argument and returns a sentence of all possible two-letter words made of letters from the argument word, allowing duplicates, like this:

~~~ scheme
> (pairs 'toy)
(tt to ty ot oo oy yt yo yy)
~~~

***

~~~ scheme
(define (pairs wd)
  (every-pair wd wd))

(define (every-pair orig-wd remaining-ltrs)
  (if (empty? remaining-ltrs)
      '()
      (se (every-permutation (first remaining-ltrs) orig-wd)
          (every-pair orig-wd (bf remaining-ltrs)))))

(define (every-permutation ltr wd)
  (if (empty? wd)
      '()
      (se (word ltr (first wd)) (every-permutation ltr (bf wd)))))
~~~

We could also define the helper procedures in tail-recursive form:

~~~ scheme
(define (pairs wd)
  (every-pair wd wd '()))

(define (every-pair original-wd remaining-wd output-sent)
  (if (empty? remaining-wd)
      output-sent
      (every-pair original-wd (bf remaining-wd) (se output-sent (every-permutation (first remaining-wd) original-wd '())))))

(define (every-permutation ltr source output-wd)
  (if (empty? source)
      output-wd
      (every-permutation ltr (bf source) (se output-wd (word ltr (first source))))))
~~~

