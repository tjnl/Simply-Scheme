### 14.13

What does the `pigl` procedure from Chapter 11 do if you invoke it with a word like “frzzmlpt” that has no vowels? Fix it so that it returns “frzzmlptay”.

***

The original procedure is:

~~~ scheme
(define (pigl wd)
  (if (member? (first wd) 'aeiou)
      (word wd 'ay)
      (pigl (word (bf wd) (first wd)))))
~~~

If invoked with `'frzzmlpt`, it will never return a value – the procedure will recursively cycle through the letters of the word indefinitely.

One option to modify `pigl` would be to add to the base case a test for whether the argument contains vowels, using a helper procedure.

~~~ scheme
(define (pigl wd)
  (if (or (member? (first wd) 'aeiou)
          (not (contains-vowel? wd)))
      (word wd 'ay)
      (pigl (word (bf wd) (first wd)))))

(define (contains-vowel? wd)
  (cond ((empty? wd) #f)
        ((member? (first wd) 'aeiou) #t)
        (else (contains-vowel? (bf wd)))))
~~~

This would be inefficient in the case of `'schnitzel` as the `contains-vowel?` procedure would be invoked four times (for `'s`, `'c`, `'h`, and `'n`), when ideally we would only want to call it once.

Alternatively, we could employ a helper procedure with a counter to keep track of where we are in the word, returning a result when either:

* the first letter of the argument is a vowel
* every letter of the word has been checked.

~~~ scheme
(define (pigl wd)
  (pigl-helper wd (count wd) 1))
  
(define (pigl-helper wd wd-length ctr)
  (cond ((member? (first wd) 'aeiou) (word wd 'ay))
        ((= ctr wd-length) (word (bf wd) (first wd) 'ay))
        (else (pigl-helper (word (bf wd) (first wd)) wd-length (+ 1 ctr)))))
~~~

