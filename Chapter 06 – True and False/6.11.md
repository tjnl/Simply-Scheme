### 6.11

Write a predicate `valid-date?` that takes three numbers as arguments, representing a month, a day of the month, and a year. Your procedure should return `#t` if the numbers represent a valid date (e.g. it isnâ€™t the 31st of September). February has 29 days if the year is divisible by 4, except that if the year is divisible by 100 it must also be divisible by 400.

~~~ scheme
> (valid-date? 10 4 1979)
#t

> (valid-date? 20 4 1776)
#f

> (valid-date? 5 0 1992)
#f

> (valid-date? 2 29 1900)
#f

> (valid-date? 2 29 2000)
#t
~~~

***

~~~ scheme
(define (valid-date? mm dd yyyy)
  (and (valid-year? yyyy) (valid-month? mm) (valid-day-of-month? dd mm yyyy)))

(define (valid-year? yyyy)
  (and (integer? yyyy) (>= yyyy 1)))

(define (valid-month? mm)
  (and (integer? mm) (>= mm 1) (<= mm 12)))

(define (valid-day-of-month? dd mm yyyy)
  (and (integer? dd)
       (>= dd 1)
       (or (<= dd 28)                                       ;; any month can have up to 28 days
           (and (<= dd 30) (not (= mm 2)))                  ;; any month except Feb can have up to 30 days
           (and (= dd 31) (not (member? mm '(2 4 6 9 11)))) ;; any month except those listed can have 31 days
           (and (= dd 29) (leap-year? yyyy)))))             ;; otherwise if it's the 29th, it must be a leap year

(define (leap-year? yyyy)        ;; to be a leap year
  (if (= 0 (remainder yyyy 100)) ;; if the year is divisble by 100
      (= 0 (remainder yyyy 400)) ;; then it must also be divisible by 400
      (= 0 (remainder yyyy 4)))) ;; otherwise it must be divisible by 4   
~~~