### 6.14

Write a procedure `describe-time` that takes a number of seconds as its argument and returns a more useful description of that amount of time:

~~~ scheme
> (describe-time 45)
(45 seconds)

> (describe-time 930)
(15.5 minutes)

> (describe-time 30000000000)
(9.506426344298686 centuries)
~~~

***

We create a set of definitions to make things more readable:

~~~ scheme
(define minute 60.0)
(define hour (* minute 60))
(define day (* hour 24))
(define week (* day 7))
(define year (* day 365.25))
(define century (* year 100))
~~~

Then we create a conditional procedure that returns the most appropriate unit of time:

~~~ scheme
(define (describe-time n)
  (cond ((<= n minute) (se n 'seconds))
        ((<= n hour) (se (/ n minute) 'minutes))
        ((<= n day) (se (/ n hour) 'hours))
        ((<= n week) (se (/ n day) 'days))
        ((<= n year) (se (/ n week) 'weeks))
        ((<= n century) (se (/ n year) 'years))
        (else (se (/ n century) 'centuries))))
~~~

We could get a bit fancy and make the conditional procedure return an even more useful definition, as in Exercise 12.13. (In fact, we are writing its recursive function “by hand”, and so this is going to be handy when we get to it.)

~~~ scheme
> (describe-time 4967189641)
'(1 centuries 57 years 20 weeks 6 days 8 hours 54 minutes 1 seconds)
~~~

Once again, we create a set of definitions to make things more readable (note that this time `minute` and `year` are defined as exact numbers so that our calculations don’t end up with an unwanted decimal place):

~~~ scheme
(define minute 60)
(define hour (* minute 60))
(define day (* hour 24))
(define week (* day 7))
(define year (* day (+ 365 1/4)))
(define century (* year 100))
~~~

Then we create a conditional procedure which finds out the most appropriate starting unit, and then a set of helper procedures that:

* check that the duration supplied isn’t more appropriately expressed in the next-smallest unit, and if so, pass the duration to the helper procedure that handles that unit; otherwise
* return a sentence with the time in that unit, plus
* pass the remainder to the next-smallest unit’s helper procedure.

If the remainder is `0`, the `seconds` helper procedure eventually returns “exactly”.

~~~ scheme
(define (describe-time n)
  (cond ((<= n minute) (seconds n))
        ((<= n hour) (minutes n))
        ((<= n day) (hours n))
        ((<= n week) (days n))
        ((<= n year) (weeks n))
        ((<= n century) (years n))
        (else (centuries n))))

(define (seconds n)
  (cond ((> n 0) (se n 'seconds))
        (else 'exactly)))

(define (minutes n)
  (if (= 0 (floor (/ n minute)))
      (seconds (remainder n minute))
      (se (floor (/ n minute)) 'minutes (seconds (remainder n minute)))))

(define (hours n)
  (if (= 0 (floor (/ n hour)))
      (minutes (remainder n hour))
      (se (floor (/ n hour)) 'hours (minutes (remainder n hour)))))

(define (days n)
  (if (= 0 (floor (/ n day)))
      (hours (remainder n day))
      (se (floor (/ n day)) 'days (hours (remainder n day)))))

(define (weeks n)
  (if (= 0 (floor (/ n week)))
      (days (remainder n week))
      (se (floor (/ n week)) 'weeks (days (remainder n week)))))

(define (years n)
  (if (= 0 (floor (/ n year)))
      (weeks (remainder n year))
      (se (floor (/ n year)) 'years (weeks (remainder n year)))))

(define (centuries n)
  (if (= 0 (floor (/ n century)))
      (years (remainder n century))
      (se (floor (/ n century)) 'centuries (years (remainder n century)))))
~~~